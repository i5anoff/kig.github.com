<!doctype html>
<html>
	<head>
		<meta charset="utf-8">
		<style>
			html,
			body {
				width: 100%;
				height: 100%;
				background: black;
			}
			* {
			  margin: 0;
			}
			body {
				display: flex;
				align-items: center;
				justify-content: center;
				background: #333639;
			}
			body > canvas {
				display: flex;
				box-shadow: 0px 0px 40px rgba(0,0,0,0.4);
			}
			#weather-data {
				/*display: none;*/
				color: white;
				position: absolute;
				top: 50%;
				margin-top: -164px;
				left: 0px;
				font-family: 'Proxima Nova', Helvetica Neue, Segoe UI, sans-serif;
				font-weight: 100;
				font-size: 36px;
				transition: 0.5s;
				text-align: center;
				width: 100%;
				text-shadow: 0px 0px 30px rgba(0,0,0,0.2);
			}
			#city {
				font-size: 84px;
				margin-bottom: 8px;
			}
			#temperature {
				font-size: 48px;
			}
			.fade-out {
				opacity: 0;
			}
			#wind-direction {
				display: inline-block;
				vertical-align: middle;
				margin-left: 8px;
			}

			#come-birds,
			#fly-away,
			#i-see-you
			{
				position: absolute;
				text-align: center;
				left: 50%;
				top: 100px;
				width: 1000px;
				margin-left: -500px;
				font-size: 84px;
				color: white;
				text-shadow: 0px 0px 30px rgba(0,0,0,0.2);
				font-family: 'Proxima Nova', Helvetica Neue, Segoe UI, sans-serif;
				font-weight: 100;
				transition: 0.5s;
			}

			#i-see-you
			{
				top: 10px;
				font-size: 36px;
			}

			#cities {
				position: absolute;
				top: 10px;
				left: 10px;
				opacity: 0;
				transition: 1s;
			}

			#cities:hover {
				opacity: 1;
			}

			#city-names {
				width: 200px;
				height: 200px;
			}
		</style>
	</head>
	<body>
		<div id="cities">
<textarea id="city-names">London
New York
Shanghai
Mumbai
Tokyo
Paris
San Francisco
Berlin
Rio de Janeiro
Moscow</textarea>
		</div>
		<div id="come-birds" class="fade-out">
			Birdy birdy come come...
		</div>
		<div id="fly-away" class="fade-out">
			Fly away!
		</div>
		<div id="i-see-you" class="fade-out">
			I see you...
		</div>
		<div id="weather-data">
			<div id="city">London</div>
			<div id="temperature">7 Â°C</div>
			<div><span id="wind-speed">wind 2.1 m/s</span> from <span id="wind-direction"><svg xmlns='http://www.w3.org/2000/svg' height='60' width='60'><g id='wind-direction-arrow' transform='rotate(220 30 30)'><line x1='30' y1='30' x2='60' y2='30' style='stroke:white;stroke-width:2;'/><circle cx='54' cy='30' r='6' style='fill:white;'/></g><circle cx='30' cy='30' r='25' style='stroke:white;stroke-width:2;fill:none;'/></svg></span></div>
			<div id="cloud-cover">cloud cover 0%</div>
			<div id="weather-desc"></div>
		</div>
		<script type="text/glsl-vertex" id="corn-vert">
			attribute float afStalkIndex;
			
			uniform float ufGlobalTime;
			uniform float ufWindDirection;
			uniform float ufWindStrength;

			uniform float ufImpulse;
			uniform float ufGrassHeight;

			varying vec3 vpos;
			varying vec3 vnor;

			void main() {
				vec4 p = vec4(position, 1.0);
				vpos = position;
				vnor = normal;
				float a = 0.009*afStalkIndex+ufGlobalTime;
				float r = 0.5*pow(p.y/8.0, 2.0);
				float mag = (sin(afStalkIndex+ufGlobalTime*0.1) + r * (1.0-0.2*cos(a*4.0))) * 10.0 * ufWindStrength;
				float dir = ufWindDirection;

				float impulse = ufImpulse;
				p.x += mag * impulse * 16.0 * (normalize(position).x);
				p.y *= 1.0 + impulse * mag;
				p.z += mag * impulse * 16.0 * 1.0;

				p.y *= ufGrassHeight;
				if (ufGrassHeight < 0.01) {
					p.x = p.z = p.y = 0.0;
				}

				p.x += mag * cos(dir);
				p.z += mag * sin(dir);


				//p.y += r * cos(a);

				gl_Position = projectionMatrix * modelViewMatrix * p;
			}
		</script>
		<script type="text/glsl-fragment" id="corn-frag">
			precision highp float;
			precision mediump int;

			uniform float ufGlobalTime;
			uniform vec2 uv2Resolution;
			uniform mat4 um4CameraMatrix;
			uniform vec3 uv3CameraPosition;

			uniform float ufCloudCover;
			uniform float ufRainAmount;
			uniform float ufSunPosition;
			uniform float ufWindDirection;
			uniform float ufWindStrength;
			uniform float ufGrassHeight;

			uniform sampler2D usRandomTex;

			varying vec3 vpos;
			varying vec3 vnor;

			float noise( vec3 x )
			{
			    vec3 p = floor(x);
			    vec3 f = fract(x);
				f = f*f*(3.0-2.0*f);
				
				vec2 uv = (p.xy+vec2(37.0,17.0)*p.z) + f.xy;
				vec2 rg = texture2D( usRandomTex, (uv + 0.5)/256.0, -100.0 ).yx;
				return mix( rg.x, rg.y, f.z );
			}

			float map(vec3 p) {
			    vec3 q = vec3(0.2)*p + vec3(-cos(ufWindDirection), -0.2, -sin(ufWindDirection))*(ufGlobalTime*20.0*ufWindStrength);
			    float n = 0.0, f = 0.5;
			    n += f*noise(q); q *= 0.501; f *= 0.503;
			    n += f*noise(q); q *= 0.502; f *= 0.502;
				n += f*noise(q); 
			    return sqrt(n);
			}


			float scene(vec3 p)
			{
				return p.y-(-2.0);
			}

			float aspect = uv2Resolution.x/uv2Resolution.y;
			vec3 bgLight = normalize(vec3(
		        cos(ufSunPosition * 3.14159)*0.2*aspect,
				-sin(ufSunPosition * 3.14159)*0.1*aspect,
		        1.0
			));

			vec3 shadeBg(vec3 nml)
			{
				float skyPow = dot(nml, vec3(0.0, -1.0, 0.0));
				float sunDot = dot(bgLight, nml) * (skyPow > 0.0 ? 1.0 : smoothstep(-0.6, 0.0, skyPow));
				float sunSize = 0.0006;
				float sunAA = sunSize / 25.0;
			    float sunD = sunDot > (1.0-sunSize) ? 1.0 - max(0.0, 1.0/sunAA * (1.0-sunSize+sunAA - sunDot)) : 0.0;
				vec3 sun = vec3(6.5, 3.5, 2.0);
			    float centerPow = 0.0; //-dot(uv,uv);
			    float horizonPow = pow(1.0-abs(skyPow), 3.0)*(5.0+centerPow);
				float sunPow = sunDot;
				float sp = max(sunPow, 0.0);
			    float scattering = clamp(1.0 - abs(2.0*(-bgLight.y)), 0.0, 1.0);
				vec3 bgCol = max(0.0, skyPow)*2.0*vec3(0.8, 0.85, 0.9);
				bgCol += vec3(0.5,0.4,0.3)*(horizonPow);
				bgCol += sun*(sunD+pow( sp, max(256.0, abs(bgLight.y)*1024.0) ));
				bgCol += vec3(0.4,0.2,0.15)*(pow( sp, 16.0) + pow( sp, max(16.0, abs(bgLight.y)*256.0) ));
			    bgCol *= mix(vec3(0.7, 0.85, 0.95), vec3(1.0, 0.45, 0.1), scattering);

			    if (nml.y < 0.0) {
				    float cloud = 0.0;
/*				    cloud += 0.2*map(nml*8.5/nml.y);
				    cloud += 0.2*map(nml*14.0/nml.y);
				    cloud += 0.2*map(nml*16.5/nml.y);
				    cloud += 0.2*map(nml*18.0/nml.y);
				    cloud += 0.2*map(nml*20.0/nml.y);
				    cloud = pow(cloud, ufCloudCover*4.0);
				    cloud *= pow(abs(nml.y), 1.0);
				    */
				    bgCol = mix(bgCol, vec3(0.95-0.4*ufRainAmount), ufCloudCover*(1.0-cloud));
				    bgCol *= 1.0-cloud*ufCloudCover;
			    } else {
				    bgCol = mix(bgCol, vec3(0.95-0.4*ufRainAmount), ufCloudCover);
			    }

			    bgCol *= 1.0 - clamp(bgLight.y*3.0, 0.0, 0.6);
				return pow(max(vec3(0.0), bgCol), vec3(2.6));
			}

			vec3 project(vec3 v, mat4 e)
			{
				float d = 1.0 / ( e[0][ 3 ] * v.x + e[1][ 3 ] * v.y + e[2][ 3 ] * v.z + e[3][ 3 ] );
				return vec3(
					( e[ 0 ][0] * v.x + e[ 1 ][0] * v.y + e[ 2 ][0]  * v.z + e[ 3 ][0] ) * d,
					( e[ 0 ][1] * v.x + e[ 1 ][1] * v.y + e[ 2 ][1]  * v.z + e[ 3 ][1] ) * d,
					( e[ 0 ][2] * v.x + e[ 1 ][2] * v.y + e[ 2 ][2] * v.z + e[ 3 ][2] ) * d
				);
			}

			void main(void)
			{
				vec2 uv = (2.0 * (gl_FragCoord.xy / uv2Resolution) - 1.0);
				vec3 d = normalize(project(vec3(uv, 1.0), um4CameraMatrix) - uv3CameraPosition);
				vec3 p = uv3CameraPosition;
			    vec3 bg = shadeBg(-d);
			    vec3 col = vec3(1.0) * dot(vnor, vec3(-bgLight.x, -bgLight.y, bgLight.z)) * shadeBg(-reflect(d, vnor)) + abs(1.0-dot(vnor, d))*bg;
			    vec4 noise = (texture2D(usRandomTex, mod(gl_FragCoord.xy/256.0, 1.0))-0.5) / 64.0;
			    float fade = min(0.9, 0.2*abs(vpos.y));
			    if (ufGrassHeight < 0.1) {
			    	fade *= ufGrassHeight / 0.1;
			    }
				gl_FragColor = vec4( ( noise.rgb + (1.0 - exp(-1.3 * col)) ), fade );
			}
		</script>
		<script type="text/glsl-vertex" id="rain-vert">
			attribute float afDropIndex;

			uniform float ufGlobalTime;
			uniform float ufWindDirection;
			uniform float ufRainAmount;
			uniform float ufWindStrength;

			varying float opacity;
			varying float rainY;

			vec3 hash3( float n )
			{
			    return fract(sin(vec3(n,n+1.0,n+2.0))*vec3(43758.5453123,22578.1459123,19642.3490423));
			}

			void main() {
				vec4 p = vec4(position, 1.0);
				rainY = p.y;
				opacity = (ufRainAmount > 0.0 ? 0.5+0.5*ufRainAmount : 0.0);
				vec3 h = (hash3(afDropIndex)-vec3(0.5)) * 2000.0;
				float fallDisplacement = mod(sin(h.y+0.001*ufGlobalTime)*120.0 - 300.0*ufGlobalTime, 150.0+(h.x)/123.456);
				vec3 velocityVec = vec3(-ufWindStrength*2.0 * cos(ufWindDirection), 1.0, -ufWindStrength*2.0 * sin(ufWindDirection)) * fallDisplacement;
				float xoff = 8.0 * sin(h.x+0.001*ufGlobalTime) * 120.0;
				float yoff = -30.0;
				float zoff = abs(cos(h.z+0.001*ufGlobalTime)) * 120.0 - 100.0;
				p.xy *= 0.1;
				float yf = 40.0 + 150.0 * ufRainAmount;
				p.y *= yf;
				p.y += yoff;
				p.x += xoff + velocityVec.x * p.y * (1.75/150.0);
				p.z += zoff + velocityVec.z * p.y * (1.75/150.0);
				p.xyz += velocityVec;
				// gl_Position = p;
				gl_Position = projectionMatrix * modelViewMatrix * p;
			}
		</script>
		<script type="text/glsl-fragment" id="rain-frag">
			precision highp float;
			precision mediump int;

			uniform float ufGlobalTime;
			varying float opacity;
			varying float rainY;

			void main(void)
			{
			    vec3 col = vec3(1.0);
				gl_FragColor = vec4(clamp((0.5*gl_FragCoord.z), 0.0, 1.0)*col, (1.0 - abs(2.0*(0.5-rainY))) * 0.2 * opacity );
			}
		</script>
		<script type="text/glsl-vertex" id="bg-vert">
			void main() {
				gl_Position = vec4(position, 1.0);
			}
		</script>
		<script type="text/glsl-fragment" id="bg-frag">
			precision highp float;
			precision mediump int;

			uniform float ufGlobalTime;
			uniform vec2 uv2Resolution;
			uniform mat4 um4CameraMatrix;
			uniform vec3 uv3CameraPosition;

			uniform float ufCloudCover;
			uniform float ufRainAmount;
			uniform float ufSunPosition;
			uniform float ufWindDirection;
			uniform float ufWindStrength;

			uniform sampler2D usRandomTex;

			float noise( vec3 x )
			{
			    vec3 p = floor(x);
			    vec3 f = fract(x);
				f = f*f*(3.0-2.0*f);
				
				vec2 uv = (p.xy+vec2(37.0,17.0)*p.z) + f.xy;
				vec2 rg = texture2D( usRandomTex, (uv + 0.5)/256.0, -100.0 ).yx;
				return mix( rg.x, rg.y, f.z );
			}

			float map(vec3 p) {
			    vec3 q = vec3(0.2)*p + vec3(-cos(ufWindDirection), -0.2, -sin(ufWindDirection))*(ufGlobalTime*20.0*ufWindStrength);
			    float n = 0.0, f = 0.5;
			    n += f*noise(q); q *= 3.001; f *= 0.333;
			    n += f*noise(q); q *= 3.002; f *= 0.332;
			    n += f*noise(q); q *= 3.003; f *= 0.331;
				n += f*noise(q);
			    return n;
			}

			vec3 shadeBg(vec3 nml)
			{
				float aspect = uv2Resolution.x/uv2Resolution.y;
				// vec2 uv = (2.0 * gl_FragCoord.xy / uv2Resolution.xy - 1.0) * vec2(aspect, 1.0);
				vec3 bgLight = normalize(vec3(
			        cos(ufSunPosition * 3.14159)*0.2*aspect,
					-sin(ufSunPosition * 3.14159)*0.1*aspect,
			        1.0
				));
				float skyPow = dot(nml, vec3(0.0, -1.0, 0.0));
				float sunDot = dot(bgLight, nml) * (skyPow > 0.0 ? 1.0 : smoothstep(-0.6, 0.0, skyPow));
				float sunSize = 0.0006;
				float sunAA = sunSize / 25.0;
			    float sunD = sunDot > (1.0-sunSize) ? 1.0 - max(0.0, 1.0/sunAA * (1.0-sunSize+sunAA - sunDot)) : 0.0;
				// vec3 bgLight = normalize(vec3(
			 //        cos(ufSunPosition * 3.14159)*1.0,
				// 	-sin(ufSunPosition * 3.14159)*0.6,
			 //        1.0
				// ));
				// float sunDot = dot(bgLight, nml);
				// float sunSize = 0.005;
				// float sunAA = sunSize / 25.0;
			 //    float sunD = sunDot > (1.0-sunSize) ? 1.0 - max(0.0, 1.0/sunAA * (1.0-sunSize+sunAA - sunDot)) : 0.0;
				vec3 sun = vec3(6.5, 3.5, 2.0);
			    float centerPow = 0.0; //-dot(uv,uv);
			    float horizonPow = pow(1.0-abs(skyPow), 3.0)*(5.0+centerPow);
				float sunPow = sunDot;
				float sp = max(sunPow, 0.0);
			    float scattering = clamp(1.0 - abs(2.0*(-bgLight.y)), 0.0, 1.0);
				vec3 bgCol = max(0.0, skyPow)*2.0*vec3(0.8, 0.85, 0.9);
				bgCol += vec3(0.3,0.4,0.5)*(horizonPow);
				bgCol += sun*(sunD+pow( sp, max(256.0, abs(bgLight.y)*1024.0) ));
				bgCol += vec3(0.4,0.2,0.15)*(pow( sp, 16.0) + pow( sp, max(16.0, abs(bgLight.y)*256.0) ));
			    bgCol *= mix(vec3(0.7, 0.85, 0.95), vec3(1.0, 0.45, 0.1), scattering);

			    if (nml.y < 0.0) {
				    float cc = 0.0;
				    if (ufCloudCover < 0.1) {
				        cc = 0.0 + 0.2 * (ufCloudCover - 0.0)/0.1;
				    } else if (ufCloudCover < 0.2) {
				        cc = 0.2 + 0.1 * (ufCloudCover - 0.1)/0.1;
				    } else if (ufCloudCover < 0.3) {
				        cc = 0.3 + 0.1 * (ufCloudCover - 0.2)/0.1;
				    } else if (ufCloudCover < 0.5) {
				        cc = 0.4 + 0.1 * (ufCloudCover - 0.3)/0.3;
				    } else if (ufCloudCover < 0.75) {
				        cc = 0.5 + 0.2 * pow((ufCloudCover - 0.5)/0.25, 2.0);
				    } else {
				        cc = 0.7 + 0.75 * pow((ufCloudCover - 0.75)/0.25, 2.0);
				    }
				    float cloud = 0.0;
				    cloud += min(1.0, (1.0-smoothstep(0.0, cc, map(nml*5.0/nml.y)))) * 0.8;
				    cloud += min(1.0, (1.0-smoothstep(0.0, cc, map(nml*15.0/nml.y)))) * 0.3;
				    cloud *= pow(abs(nml.y), 1.0);
				    bgCol = mix(bgCol, vec3(0.95-0.4*ufRainAmount), cloud);
				    bgCol *= 1.0+cloud;
			    } else {
				    bgCol = mix(bgCol, vec3(0.95-0.4*ufRainAmount), ufCloudCover);
			    }

			    bgCol *= 1.0 - clamp(bgLight.y*3.0, 0.0, 0.6);
				return pow(max(vec3(0.0), bgCol), vec3(2.6));
			}

			vec3 project(vec3 v, mat4 e)
			{
				float d = 1.0 / ( e[0][ 3 ] * v.x + e[1][ 3 ] * v.y + e[2][ 3 ] * v.z + e[3][ 3 ] );
				return vec3(
					( e[ 0 ][0] * v.x + e[ 1 ][0] * v.y + e[ 2 ][0]  * v.z + e[ 3 ][0] ) * d,
					( e[ 0 ][1] * v.x + e[ 1 ][1] * v.y + e[ 2 ][1]  * v.z + e[ 3 ][1] ) * d,
					( e[ 0 ][2] * v.x + e[ 1 ][2] * v.y + e[ 2 ][2] * v.z + e[ 3 ][2] ) * d
				);
			}

			float scene(vec3 p)
			{
				return p.y-(-2.0)-map(p)*0.001;
			}

			vec3 normal(vec3 p, float d)
			{
				float e = 0.05;
				float dx = scene(vec3(e, 0.0, 0.0) + p) - d;
				float dy = scene(vec3(0.0, e, 0.0) + p) - d;
				float dz = scene(vec3(0.0, 0.0, e) + p) - d;
				return normalize(vec3(dx, dy, dz));
			}

			void main(void)
			{
				vec2 uv = (2.0 * (gl_FragCoord.xy / uv2Resolution) - 1.0);
				vec3 d = normalize(project(vec3(uv, 1.0), um4CameraMatrix) - uv3CameraPosition);
				vec3 p = uv3CameraPosition;
			    vec3 tr = vec3(1.0);
			    if (d.y < 0.0) {
			        float dist = -2.0 / d.y - p.y / d.y; // p.y + d.y * dist = -2.0;
			        p += d * dist;
			        vec3 nml = normal(p, 0.0);
			        float f = pow(1.0-dot(d, -vec3(0.0, 1.0, 0.0)), 5.0);
			        nml = mix(nml, vec3(0.0, 1.0, 0.0), f);
			        d = reflect(d, nml);
			        tr *= mix(0.5*vec3(0.5, 0.9, 0.75), vec3(1.0), f);
			    }
			    vec3 col = tr * shadeBg(-d);
			    vec4 noise = (texture2D(usRandomTex, mod(gl_FragCoord.xy/256.0, 1.0))-0.5) / 64.0;
				gl_FragColor = vec4(noise.rgb + (1.0 - exp(-1.3 * col)), 1.0 );
			}
		</script>
		<script src="js/lib/three.js"></script>
		<script src="js/corngeneration.js"></script>
		<script src="js/corn.js"></script>
		<script src="js/rain.js"></script>
		<script src="js/weatherdata.js"></script>
		<script src="js/weathertick.js"></script>
		<script src="js/particles.js"></script>
		<script src="js/birds.js"></script>
		<script src="js/impacts.js"></script>
		<script src="js/kinect-ws.js"></script>
		<script src="js/microphone.js"></script>
		<script src="js/corn_algo_doodle.js"></script>
	</body>
</html>
